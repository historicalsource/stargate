	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>

* MESSAGE HANDLER

* X POINTS AT MESSAGE DESCRIPTOR, D AND Y HOLD ANY RELEVANT DATA.
*
* MESSAGE DESCRIPTOR:
*
* PRIORITY (0-FF) EQUAL PRIORITY WILL BE DISPLAYED (P)
*
* TIME (0-FF) INDICATES DURATION OF DISPLAY (T)
*
* MESSAGE NUMBER TO BE PASSED TO TEXT ROUTINE (N)
*
* SPECIAL CONSIDERATION BITS: (S)
*
* BIT 6 SET INDICATES THAT MESSAGE SHOULD BLINK
* NOTE THAT BLINKING VALUES INDICATED BY B AND Y
* MAY BE MODIFIED BY CHANGING MESSB, AND MESSY IN
* RAM (SAVED FROM CALL).
*
* BIT 5 SET SAYS THAT 2 BYTE IMMEDIATE MUST BE RESIDENT MESSAGE (R)
* FOR ACTION TO BE TAKEN.
*
* BIT 4 SET INDICATES FOLLOWER MESSAGE PRESENT (SAME FORM WITHOUT
* PRIORITY BYTE).
*
* BIT 3 INDICATES THAT A 2 BYTE IMMEDIATE WILL POINT TO NEXT BLOCK (I)
* TO DO IMMEDIATELY (INDIRECT CONTINUATION). THE
* MESSAGE POINTED TO WILL BECOME THE MESSAGE CONSIDERED
* FOR RESIDENT MESSAGE REQUIREMENT.
*
* BIT 2 INDICATES MESSAGE AREA SHOULD BE CLEARED BEFORE DISPLAY.
*
* ENTRY TO MESSLD:
*
* X REGISTER ---> (P),(S),[(R)],(T),(N),[(I)],
* [ (S),[(R)],(T),(N),[(I)] ] *N
*
*
*
* STATE OF SYSTEM :
*
* MESSCR - CURRENT PLACE IN MESSAGE. 2 BYTES
* MESSST - BEGINNING OF THIS MESSAGE. 2 BYTES
* MESSSP - SPECIAL CONSIDERATIONS 1 BYTE
* MESSTM - TIME LEFT IN CURRENT FRAME 1 BYTE
* MESSBL - MESSAGE BLINK RATE 1 BYTE
* MESSBC - MESSAGE BLINK COLOR 1 BYTE
* MESSCD - CODE OF CURRENT MESSAGE 1 BYTE
* MESSB - SAVE B FOR CONTINUATION ETC 1 BYTE
* MESSY - SAVE Y FOR POSSIBLE CONTIN. 2 BYTES
*

BLKTIM	EQU	$18	BLINK TIMER.

	ORG	MESORG

	JMP	MESSLD	LOADER
	JMP	MESSIN	INITIALIZER

* UPDATE

MESSUP	LDA	STATUS	OK TO DO IT??
	BITA	#$2C	NOT IF OJBECTS OR SAM TURNED OFF, OR PLAYER DEAD
	BNE	MESSUX
	LDA	MESSTM	GET TIMER
	BEQ	MESSUX	NOTHING THERE...WHAT A WASTE OF TIME
	DEC	MESSTM	SO KNOCK IT DOWN ONE
	BNE	MESSUX	NOT DONE HERE
	LDA	MESSSP	GET THE SPECIAL STUFF
	BITA	#CONT	CONTINUATION??
	BEQ	MESSU1	NOPE
	LDB	MESSB	RESTORE THE POSSIBLE NUMBER PARAMS
	LDY	MESSY
	LDX	MESSST	LOAD X WITH THE MESSAGE STARTER
	JMP	MESSCV	DO PHONEY CALL AND RETURN.
MESSU1	LDX	#0	CLEAR START POINTER SO NO SATISFACTION OF CONDITION
	STX	MESSST	DONE.
MESIN1	BSR	CLMESS	CLEAR OUT THE REGION
	LDA	#DEFMES	PUT DEFAULT MESSAGE OUT.
	JMP	WRD5FS	AND RETURN.
MESSUX	RTS	EXIT

MESSU6	EQU	*
CLMESS	PSHS	X,A,U,Y	CLEAR MESSAGE AREA
	LDX	#0
	LEAY	,X
	CLRA
	LDU	#MESSUL+5
CLMES0	PSHU	X,Y,A
	LEAU	261,U	NEXT COLUMN AND DOWN THE 5 BYTES
	CMPU	#MESSLR+$100 BEYOND LOWER RIGHT??
	BLO	CLMES0
	PULS	X,A,U,Y,PC

MESSIN	LDD	#0
	STD	MESSST	NO CURRENT MESSAGE
	STA	MESSTM	NO TIME IN CURRENT FRAME
	BRA	MESIN1	PUT DEFAULT MESSAGE UP ETC.

MESSCV	PSHS	D,X	DO BUNKO PUSH
	LDX	MESSCR	GET THE CURRENT POINTER
	BRA	MESEN2	GO TO SECOND ENTRYPOINT IN MESSLD
MESSLD	PSHS	X,D	SAVE STATE
	LDA	MESSTM	ANY MESSAGE THERE???
	BEQ	MESS1	NOPE.....PROCEED
	LDA	,X	GET PRIORITY
	CMPA	[MESSST] COMPARE TO CURRENT MESSAGES PRIORITY
	BLO	MESSX	NOT GOOD ENOUGH....BYE
MESS1	LEAX	1,X	INX PUSH PAST PRIORITY
MESEN2	LDA	,X+	GET THE SPECIAL CONSIDERATIONS
	STA	MESSSP	AND PUT THEM IN VISIBILITY RANGE
	BITA	#COND	SEE IF CONDITIONAL THERE
	BEQ	MESS3	NOPE
	LDD	,X++	GET CONDITIONAL
	CMPD	MESSST	SEE IF THAT IS THE RESIDENT MESSAGE
	BNE	MESSX	NOPE...BYE
MESS3	LDA	,X+	GET TIME
	STA	MESSTM	AND SAVE
	LDA	MESSSP	LOOK AT SPECIAL BYTE
	BITA	#CLEAR	DO WE CLEAR THE REGION
	BEQ	MESS4	NOPE
	BSR	CLMESS	CLEAR IT
MESS4	LDA	,X+	GET CODE
	STA	MESSCD	SAVE
	STX	MESSCR	SAVE POINTER
	LDB	1,S	RESTORE PASSED X,D
	LDX	2,S
	STB	MESSB	SAVE FOR BLINK
	STY	MESSY	SAVE FOR BLINK
	STX	MESSST	INDICATE CURRENT MESSAGE
	JSR	WRD5FS	PRINT THE MESSAGE
	LDA	MESSSP	CHECK SPECIAL BIT
	BITA	#PTR	DO WE POINT TO CONTINUATION
	BEQ	MESS2	NOPE
	LDX	MESSCR	GET CURRENT POINTER
	LDX	,X	GET PLACE TO CONTINUE
	STX	MESSST	MAKE PLACE WE GOING SATISFY CONDITIONAL
	JSR	MESSLD	ISN RECURSION WONDERFUL??
MESS2	EQU	*
	LDA	MESSSP	GET SPECIALS
	BITA	#BLINK	BLINK??
	BEQ	MESSX	NOPE
	MAKP	BLPROK	START A PROCESS TO DO SO
MESSX	PULS	X,D,PC	CLEANUP AND RETURN FOR BULLSHIT EXIT

BLPROK	LDD	MESSST	GET ID OF CURRENT MESSAGE
	STD	PD,U	AND SAVE
BLPR1	LDA	MESSBC	GET THE BLINK COLOR
	STA	PCRAM+5	MESSAGE BLINK COLOR
	LDA	MESSBL	GET BLINK RATE
	LDX	#BLPR2
	JMP	SLEEP
BLPR2	LDD	MESSST
	CMPD	PD,U	SAME MESSAGE CURRENT??
	BNE	BLPRX	BYE....
	CLR	PCRAM+5
	LDA	MESSBL
	LDX	#BLPR1
	JMP	SLEEP
BLPRX	JMP	SUCIDE

WRD5FS	JSR	WRD5FV
	LDA	#1
	STA	TIMER	STOP MESSAGE RELATED HYPERS
	RTS

	END
